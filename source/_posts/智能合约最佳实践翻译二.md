---
title: 智能合约最佳实践翻译二
comments: true
date: 2018-03-02 17:04:23
categories: 区块链
tags: 
- ZhouFyk 
- 以太坊 
- 智能合约 
- 安全开发
img:
---

[原文链接](https://consensys.github.io/smart-contract-best-practices/recommendations/)

## 对 Solidity 中的智能合约的安全推荐 Recommendations for Smart Contract Security in Solidity

本页面展示了编写智能合约时应遵循的一些 solidity 模式。

### 协议具体建议

以下建议适用于以太坊中所有的智能合约的开发。

### 外部调用

#### 小心使用外部调用

调用不受信任的合约会引入一些预期外的风险或错误。外部调用可能会在该合约或其依赖的任何其他合约中执行恶意代码。所以每个外部调用都要被当做潜在的安全风险来对待。如果不可能或不希望删除外部调用，请使用本节其余部分的建议来减少危险

#### 标记不受信任的合约

当与外部合约交互的时候，通过某种形式命名你的变量，方法和合约接口来清楚地表达其所交互的对象是具有潜在性的安全问题的。这适用于你的调用外部合约的函数。

```
// bad
Bank.withdraw(100); // Unclear whether trusted or untrusted

// Isn't clear that this function is potentially unsafe
function makeWithdrawal(uint amount)  {
		Bank.withdraw(amount);
}

// good
UntrustedBank.withdraw(100);
TrustedBank.withdraw(100);

function makeUntrustedWithdrawal(uint amount) {
		UntrustedBank.withdraw(amount);
}
```

#### 避免在外部调用之后改变状态

不论是否使用 `raw calls`（`someAddress,call()` 的格式） 或者 `contract calls`（`ExternalContract.someMethod()` 的格式），都假定会执行恶意代码。即使 `ExternalContract` 不是恶意的，但它调用的任何合约也可能会执行恶意代码。

一个特殊的危险是，恶意代码可能对控制流进行了 `hijack` 攻击，导致条件竞争。（见 [Race Conditions](https://consensys.github.io/smart-contract-best-practices/known_attacks/#race-conditions) 了解更多关于这个问题的讨论）。

如果你调用了一个不受信任的外部合约，*避免在调用之后改变状态*。这个模式也常常作为 [check-effects-interactions 模式](https://solidity.readthedocs.io/en/develop/security-considerations.html?highlight=check%20effects#use-the-checks-effects-interactions-pattern) 为人们所知。

#### 注意 `send() / transfer() / call.value()` 之间的平衡

在发送 ether 时注意 `someAddress.send() / someAddress.transfer() / someAddress.call.value()()` 之间的使用的关系的平衡。

* `someAddress.send()` 和 `someAddress.transfer()` 被认为对于[重入](https://consensys.github.io/smart-contract-best-practices/recommendations/#reentrancy)是安全的。尽管这些方法仍然会触发代码的执行，但是被调用的合约只会被提供 2300 单位的燃料，目前来说，这么多的燃料足够用来记录事件了。
* `x.transfer(y)` 和 `require(x.send(y));` 的等同的，如果发送失败，它会自动回退。
* `someAddress.call.value(y)()` 会发送提供的以太币，然后触发代码的执行。为执行的代码提供所有可用来执行的燃料使得这种转账方式对于重入来说很不安全。

使用 `send()` 和 `transfer()` 可以防止重入，但是这样的做法会使得与那些 fallback 函数的消耗超过 2300 单位燃料的合约不兼容。也可以使用 `someAddress.vall.value(ethAmount).gas(gasAmount)()` 来转发指定的燃料数量。

一个试图平衡这个问题的解决办法是实现 *[退和拉](https://consensys.github.io/smart-contract-best-practices/recommendations/#favor-pull-over-push-payments)* 的机制模式， *推送* 部分使用 `send()` 或者 `transfer()` ，*拉* 部分使用 `call.value()` 。

值得指出的是，对于价值转移而言，专用 `send()` 或者 `transfer()` 并不能使得合约避免重入的安全问题，但是能够使得一些特定的转账避免重入的安全问题。

#### 解决外部调用的错误

Solidity 提供了处理原生地址的底层调用方法：`address.call()`，`address.callcode()`，`address,delegatecall()`，和 `address.send()`。当调用遇到一个异常的时候，这些底层方法不会抛出一个异常，而是返回一个 `false`。从另一方面来说，*合约调用*（如 `ExternalContract.doSomething()`）会自动传播一个异常（比如，如果 `doSomething()` 抛出异常，`ExternalContract.doSomething()` 也会 `throw`）。

如果你选择使用底层调用方法，要确定你通过检查返回值，处理了可能存在的调用失败的情况。

```
// bad
someAddress.send(55);
someAddress.call.value(55)(); // this is doubly dangerous, as it will forward all remaining gas and doesn't check for result
someAddress.call.value(100)(bytes4(sha3("deposit()"))); // if deposit throws an exception, the raw call() will only return false and transaction will NOT be reverted

// good
if (!someAddress.send(55)) {
		// some failure code
}

ExternalContract(someAddress).deposit.value(100);
```

#### 对外部调用来说，拉比推更好

外部调用可能有意无意而失败。为了最小化这样失败带来的破坏，通常最好将每个外部调用隔离到可以由调用接收方发起的自己的交易中。这与支付特别相关，与将资金自动推给用户比起来，更好的是让用户提取资金。（这也减少了[燃料限制问题](https://consensys.github.io/smart-contract-best-practices/known_attacks/#dos-with-block-gas-limit)。）避免在单个交易中组合多个 `send()` 

```
// bad
contract auction {
	address highestBidder;
	uint highestBid;

	function bid() payable {
		require(msg.value >= highestBid);

		if (highestBidder != 0) {
			highestBidder.transfer(highestBid); // if this call consistently fails, no one else can bid 
		}

		highestBidder = msg.sender;
		highestBid = msg.value;
	}
}

// good
contract auction {
	address highestBidder;
	uint highestBid;
	mapping(address => uint) refunds;

	function bid() payable external {
		require(msg.value >= highestBid);

		if (highestBidder != 0) {
			refunds[highestBidder] += highestBid; // record the refund that this user can claim
		}

		highestBidder = msg.sender;
		highestBid = msg.value;
	}

	function withdrawRefund() external {
		uint refund = refunds[msg.sender];
		refunds[msg.sender] = 0;
		msg.sender.transfer(refund);
	}
}
```

#### 不要假设合约创建时是 0 资产

一个攻击者可以在合约创建之前发送 wei 到合约的地址。合约不应该假设初始状态包含 0 资产。见 [问题 61](https://github.com/ConsenSys/smart-contract-best-practices/issues/61) 查看更多细节。

#### 记住链上的数据是公开的

许多应用程序要求提交数据以便在某个时间点可以供其工作。游戏（如链上的石头剪子布）和拍卖机制（如 盲派的第二次出价）是两种主要类别的例子。如果你正在构建一个涉及到隐私的应用，注意不免要求用户过早发布信息。

示例：
* 在石头剪子布中，首先要求两个玩家同时提交他们的出手意图的哈希值，然后再要求提交他们的实际出手，如果匹配不上之前的哈希值，则抛出。
* 在一个拍卖中，要求玩家在初始阶段提交他们的出价金额的哈希值（保证金高于他们的竞价），然后在第二阶段提交他们的实际竞价。
* 当开发一个依赖于随机数生成器的应用时，顺序应该总是（1）提交行动，（2）生成随机数，（3）玩家付出。随机数生成方法本来就是一个活跃的研究领域；目前同类最佳的解决方案包括比特币区块头（通过 [http://btcrelay.org](http://btcrelay.org) 来验证），散列提交揭示方案（即一方产生一个数字，将其散列值发布为”提交“的值，然后揭示这个数）和 [RANDAO](https://github.com/randao/randao)
* 如果你正在实现一个频繁的批量拍卖，哈希提交方案也很合适。

### 注意在双方或者多方合约中，一些成员可能”掉线“，没有返回

不要只依赖于特定方执行特定行动的退款或者索赔流程，而没有其他获得资金的方式。比如，在剪刀石头布游戏中，一个常见的错误是在两位玩家提交它们的行动之前，没有进行支付；然而，一个恶意的玩家可以简单地通过不提交他的动作来使得另一位玩家陷入困境 - 实际上，如果一个玩家看到了其他玩家暴露的行为，并且确定他们输了，他们就没有任何理由去提交他们自己的行为。这个问题也可能出现在状态渠道的解决中。当这样的场景变成一个问题，（1）提供一种规避未参与的参与者的方式，可能是通过限制时间，（2）考虑增加额外的经济激励，让参与者在需要提交信息的场景中提交所有该场景需要的信息。

### Solidity 详细建议

下面的建议对于 Solidity 来说十分详细，不过对于使用其他语言来开发智能合约来说，应该也是有所帮助的。

### 强制对不变值使用 `assert()`

当一个断言失败的时候，它的保护机制就会被触发 - 比如当一个不变的属性改变的时候。比如，在一个代币发行合约中，代币与以太币的汇率可能是固定的。然后你可以通过 `assert()` 来验证所有的这种情况。断言保护机制应该经常与其他技术结合，如暂停合约和允许升级。（否则，你可能会陷入停滞，而一个断言总是在失败。）

例子：

```
contract Token {
	mapping (address => uint) public balanceOf;
	uint public totalSupply;

	function deposit() public payable {
		balanceOf[msg.sender] += msg.value;
		totalSupply += msg.value;
		assert(this.balance >= totalSupply);
	}
}
```

注意断言并非对资产严格相等，因为合约可以不通过 `deposit` 函数强制[发送以太币](https://consensys.github.io/smart-contract-best-practices/recommendations/#ether-forcibly-sent)

### 正确地使用 `assert()` 和 `require()`

在 Solidity 0.4.10 中，引入了 `assert()` 和 `require()`。`require(condition)` 被用来对用户的任何输入进行验证，如果条件不成立则回滚。`assert(condition)` 在条件不成立时也会回滚，但是只用在不变值上：内部错误或者检查你的合约是否是非法的状态。遵循这种范式将允许形式分析工具来进行验证工作，而非法的操作码永远不会到达：这意味着代码中的任何不变值都不会被违反，并且代码正确地验证。

### 小心整数除法

所有整数除法都被向下取整。如果你需要更高的精度，考虑使用乘数，或者存储分子和分母。

（在未来，Solidity 会有定点类型，这会使得整数除法更容易。）

```
// bad
uint x = 5 / 2; // Result is 2, all integer divison rounds DOWN to the nearest integer
```

使用乘数来防止向下取整，当使用 x 的时候要考虑到这个乘数：

```
// good
uint multiplier = 10;
uint x = (5 * multipler) / 2;
```

存储分子和分母意味着你可以离链计算分子除以分母的结果。

```
// good
uint numerator = 5;
uint denominator = 2;
```

### 记住以太币可以被强制发送到一个账户

注意对严格检查了的合约的余额进行编码。

一个攻击者可以强制对任何账户发送 wei，而且这不能被阻止（即使是使用了 `revert()` 的 fallback 函数也不可以）

攻击者通过创余额为 1 wei 的合约，然后通过 `selfdestruct(victimAddress)` 的方式达到目的。`victimAddress` 中没有代码被激活，所以这不能被阻止。

### 注意抽象合约和接口的平衡

接口和抽象合约都为智能合约提供了一种定制的，可复用的方式。接口，在 Solidity 0.4.11 中被引入，与抽象合约十分相似，但是接口不能有已经实现的方法。接口同样也有限制，它不能访问存储，不能继承其他接口，而这通常使得抽象合约更加实用。虽然，接口实际上对设计合约的实现上十分有用。另外，十分重要的是如果一个合约继承了一个抽象合约，那它必须通过重写来实现所有抽象合约中未实现的函数，否则它依旧是一个抽象合约。

### 保持 fallback 函数简单

当一个合约发送了没有参数（或者当没有函数匹配上的时候）的消息时，[Fallback 函数](https://solidity.readthedocs.io/en/latest/contracts.html#fallback-function) 会被调用，而且如果通过 `.send()` 或者 `.transfer()` 调用的话只需要 2300 单位的燃料。如果你希望能够从 `.send()` 或者 `.transfer()` 接受以太币，你能做的就是在 fallback 函数中记录一个日志。如果需要计算，或者更多的燃料，就使用一个真正的函数。

```
// bad
funciton() payable { balances[msg.sender] += msg.value; }

// good
function deposit() payable external { balances[msg.sender] += msg.value; }

function() payable { LogDepositReceived(msg.sender); }
```

### 为函数和状态变量显式标记可见性

显式标记函数和状态变量的可见性。函数可以被指定为 `external / public / internal / private`。请理解它们之间的区别，比如，比起 `public`，`external` 可能已经足够使用了。对于状态变量来说，`external` 是不可能的。显式标记可见性将更容易捕捉有关谁可以调用函数或访问变量的错误假设。 

```
// bad
uint x; // the default is internal for state variables, but it should be made explicit
function buy() { // the default is public 
		// public code
}

// good
uint private y;
function buy() external {
		// only callable external
}

function utility() public {
		// callable externally, as well as internally: changing this code requires thinking about both cases
}

function internalAction() internal {
		// internal code
}
```

### 锁定指定编译器版本的编译指示

应该使用相同的编译器版本部署合同，并标记它们已经经过了足够的测试。锁定编译指示可以帮助确保合约不会意外部署，比如，最新的编译器对未知错误有更高的风险。合约也可能由其他人部署，通过编译指示就知道原作者打算使用的编译器版本。

```
// bad 
pragma solidity ^0.4.4;

// good
pragma solidity 0.4.0;
```

#### 异常

当合约打算供其他开发人员使用时，可以允许 `pragma` 语句浮动，例如类库或者 EthPM 包。否则，开发人员为了在本地编译就需要手动更新编译指示。

### 函数和事件的区别

对事件名首字母大写,以及添加作用前缀，来防止对函数和事件产生困惑。对于方法来说，总是以小写字母开头，除了构造函数之外。

```
// bad 
event Transfer() {}
function transfer() {}

// good
event LogTransfer() {}
function transfer() external {}
```

### 推荐使用更新的 Solidity 结构

使用构造/别名如 `selfdestruct` （而不是 `suicide`），和 `keccak256` （而不是 `sha3`）。`require(msg.sender.send(1 ether))` 也可以使用 `transfer()` 简单地实现： `msg.sender.transfer(1 ether)`。

### 注意内置函数可以被覆盖

目前可以在 Solidity 覆盖（[shadow](https://en.wikipedia.org/wiki/Variable_shadowing)）内置的全局变量。它允许合约重写覆盖内置的方法，如 `msg` 和 `revert()`。尽管这是[故意](https://github.com/ethereum/solidity/issues/1249)的，但是它可能会在关于合约的真实行为上误导合约的用户。

```
contract PretendingToRevert {
	function revert() internal constant {}
}

contract ExampleContract is PretendingToRevert {
	function somethingBad() public {
		revert();
	}
}
```

合约用户（和审计人员）应该注意他们想要使用的应用的所有合约源代码。

### 避免使用 `tx.origin`

永远不要使用 `tx.origin` 进行授权，另一个合约可以有一种方法来调用你的合约（比如用户有一些资金），而因为你的地址在 `tx.origin` 里面，你的合约就会授权该交易。

```
pragma solidity ^0.4.18;

contract MyContract {
	address owner;

	function MyContract() public {
		owner = msg.sender;
	}

	function sendTo(address receiver, uint amount) public {
		require(tx.origin == owner);
		receiver.transfer(amount);
	}
}

contract AttackingContract {
	MyContract myContract;
	address attacker;

	function AttackingContract(address myContractAddress) public {
		myContract = MyContract(myContractAddress);
		attacker = msg.sender;
	}

	function() public {
		myContract.sendTo(attacker, msg.sender.balance);
	}
}
```

你应该使用 `msg.sender` 来授权（如果有另一个合约调用了你的合约，`msg.sender` 会成为合约的地址，而且不是调用合约的人的地址）。

更多可见 [Solidity 文档](https://solidity.readthedocs.io/en/develop/security-considerations.html#tx-origin)

除了授权的这个问题，`tx.origin` 可能在将来从以太坊协议中移除，所以使用了 `tx.origin` 的代码在将来不会再被兼容 [Vitalik: 'Do NOT assume that tx.origin will continue to be usable or meaningful.'](https://ethereum.stackexchange.com/questions/196/how-do-i-make-my-dapp-serenity-proof/200#200)

另外值得一提的是，通过使用 `tx.origin`，你会限制合同之间的互操作性，因为使用 `tx.origin` 的合同不能被另一个合同使用，因为合同不能是 `tx.origin`。

### 时间戳依赖

在合同中使用时间戳执行关键功能时有三个主要考虑因素，特别是当涉及到资金转移时。

#### Gameability

注意区块的时间戳是可用被矿工操作的。考虑这个[合约](https://etherscan.io/address/0xcac337492149bdb66b088bf5914bedfbf78ccc18#code)：

```
uint256 constant private salt = block.timestamp;

function random(uint Max) constant private returns (uint256 result) {
	//get the best seed for randomness
	uint256 x = salt * 100/Max;
	uint256 y = salt * block.number/(salt % 5);
	uint256 seed = block.number/3 + (salt % 300) + Last_Payout + y;
	uint256 h = uint256(block.blockhash(seed));

	return uint256((h / x)) % Max + 1; // random number between 1 and Max
}
```

当合约使用时间戳来产生随机数时，矿工实际上可以在被验证块的 30 秒内发布时间戳，从而有效地允许矿工预先计算一个更有利于他们抽奖机会的选项。 时间戳不是随机的，不应该在这种情况下使用。

#### 30 秒规则

评估时间戳使用的一般经验法则是：

**如果合约功能可以容忍30秒的时间漂移，则可以安全使用 `block.timestamp`**

如果您的时间相关事件的比例可以变化 30 秒并保持完整性，则使用时间戳是安全的。 这包括拍卖结束，注册期限等事情。

#### 将 `block.number` 作为时间戳使用时要当心

当合约创建一个 `auction_complete` 修饰器来表示代币销售的结束，像[这样](https://consensys.github.io/smart-contract-best-practices/(https://github.com/SpankChain/old-sc_auction/blob/master/contracts/Auction.sol))

```
modifier auction_complete {
	require(auctionEndBlock <= block.number ||
		currentAuctionState == AuctionState.success ||
		currentAuctionState == AuctionState.cancel)
		_;
}
```

`block.number` 和 [平均出块时间](https://etherscan.io/chart/blocktime) 也可以用来估算时间，但是这不是未来的证明，因为区块时间可以修改（例如 [分叉重组](https://blog.ethereum.org/2015/08/08/chain-reorganisation-depth-expectations/) 和 [难度炸弹](https://github.com/ethereum/EIPs/issues/649)）。在一天的销售中，12分钟规则允许人们构建更可靠的时间估计。

### 弃用/历史 建议

由于协议的修改或者 solidity 的提升，这些建议不再相关。They are recorded here for posterity and awareness.

#### 注意被 0 除 （Solidity < 0.4）

在版本 0.4 之前，当一个数字除以 0 时，Solidity 会返回零，并且不会引发异常。 确保你至少运行 0.4 版本。
